apply plugin: 'com.android.application'

def buildVersionNumberBlackBerry() {
  def result = android.defaultConfig.versionName
  if (result.split('\\.').length == 4)
    return result
  else
    return result + ".0"
}


android {
    compileSdkVersion project.compileSdkVersion
    buildToolsVersion project.buildToolsVersion

    defaultConfig {
        applicationId "org.totschnig.myexpenses"
        minSdkVersion project.minSdkVersion
        targetSdkVersion project.targetSdkVersion
        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_7
            targetCompatibility JavaVersion.VERSION_1_7
        }
        versionCode 240
        versionName '2.6.5.1'

        testApplicationId "org.totschnig.myexpenses.test"
        testInstrumentationRunner "android.test.InstrumentationTestRunner"

        buildConfigField "String", "PLATTFORM", "\"Android\""
        buildConfigField "String", "BUILD_DATE", "\"" + getDate() + "\""

    }
    signingConfigs {
        release {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    buildTypes {
        debug {
            minifyEnabled true
            //shrinkResources true // need to wait for resolution
            // https://code.google.com/p/android/issues/detail?id=79325
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
        }
        release {
            minifyEnabled true
            //shrinkResources true // need to wait for resolution
            // https://code.google.com/p/android/issues/detail?id=79325
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
            signingConfig signingConfigs.release
        }
        //beta.initWith(buildTypes.release)
    }
    flavorDimensions "adprovider", "distribution"

    productFlavors {
        play {
            dimension "distribution"
        }
        amazon {
            dimension "distribution"
        }
        blackberry {
            dimension "distribution"
            versionName buildVersionNumberBlackBerry()
            buildConfigField "String", "PLATTFORM", "\"Blackberry\""
        }
        admob {
            dimension "adprovider"
        }
        ama {//Amazon Mobile Ads
            dimension "adprovider"
        }
    }


    lintOptions {
        disable 'InvalidPackage', 'MissingQuantity', 'MissingTranslation', 'ImpliedQuantity'
    }

    dexOptions {
        javaMaxHeapSize "2048M"
        jumboMode = true
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }
    //testBuildType "release"
}

dependencies {
    compile project(':StickyListHeaders:library')
    compile project(':MPAndroidChart:MPChartLib')
    compile project(':MaterialSeekBarPreference:library')
    compile project(':PlayLicensingOrig')
    compile 'com.android.support:appcompat-v7:' + project.androidSupportLibraryVersion
    compile 'com.android.support:support-v4:' + project.androidSupportLibraryVersion
    compile 'com.android.support:recyclerview-v7:' + project.androidSupportLibraryVersion
    compile 'com.android.support:cardview-v7:' + project.androidSupportLibraryVersion
    compile 'com.android.support:design:' + project.androidSupportLibraryVersion
    compile 'com.android.support:gridlayout-v7:' + project.androidSupportLibraryVersion
    compile 'com.android.support:preference-v14:' + project.androidSupportLibraryVersion
    compile 'com.itextpdf:itextpdf:5.5.8'
    compile 'ch.acra:acra:4.8.2'
    admobCompile 'com.google.android.gms:play-services-ads:8.4.0'
    compile group: 'com.google.guava', name: 'guava', version: '18.0'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'org.onepf:openiab:0.9.8.7'
    compile files('libs/in-app-purchasing-2.0.61.jar')
    compile files('libs/fortumo-in-app-9.1.2.jar')
    amaCompile 'com.amazon.android:mobile-ads:5.+'
    compile 'org.apache.commons:commons-csv:1.1'
    //noinspection GradleDynamicVersion
    compile 'com.commonsware.cwac:wakeful:1.0.+'
    androidTestCompile 'com.jayway.android.robotium:robotium-solo:5.2.1'
    testCompile 'junit:junit:4.12'
}
def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd-HHmm')
    return formattedDate
}
